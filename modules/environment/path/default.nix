{
  config,
  pkgs,
  lib,
  ...
}:
{
  options = {
    environment.systemPackages = lib.mkOption {
      type = with lib.types; listOf package;
      default = { };
    };

    environment.pathsToLink = lib.mkOption {
      type = with lib.types; listOf str;
      default = [ ];
      example = [ "/" ];
      description = "List of directories to be symlinked in {file}`/run/current-system/sw`.";
    };

    environment.extraSetup = lib.mkOption {
      type = lib.types.lines;
      default = "";
      description = "Shell fragments to be run after the system environment has been created. This should only be used for things that need to modify the internals of the environment, e.g. generating MIME caches. The environment being built can be accessed at $out.";
    };

    environment.path = lib.mkOption {
      type = lib.types.path;
      readOnly = true;
    };
  };

  config = {
    environment.systemPackages = with pkgs; [
      bzip2
      cpio
      ncurses
      util-linux
      su
      zstd
    ];

    environment.pathsToLink = [
      "/bin"
      "/etc/xdg"
      "/etc/gtk-2.0"
      "/etc/gtk-3.0"
      # NOTE: We need `/lib' to be among `pathsToLink' for NSS modules to work.
      "/lib" # FIXME: remove and update debug-info.nix
      "/sbin"

      # TODO: trim this list down?
      "/share/themes"
      "/share/vulkan"
      "/share/thumbnailers"
      "/share/wayland-sessions"
    ];

    environment.path = pkgs.buildEnv {
      name = "system-path";
      paths = config.environment.systemPackages;
      pathsToLink = config.environment.pathsToLink;

      ignoreCollisions = true;

      # !!! Hacky, should modularise.
      # outputs TODO: note that the tools will often not be linked by default
      postBuild = ''
        # Remove wrapped binaries, they shouldn't be accessible via PATH.
        find $out/bin -maxdepth 1 -name ".*-wrapped" -type l -delete

        if [ -x $out/bin/glib-compile-schemas -a -w $out/share/glib-2.0/schemas ]; then
            $out/bin/glib-compile-schemas $out/share/glib-2.0/schemas
        fi

        ${config.environment.extraSetup}
      '';
    };
  };
}
