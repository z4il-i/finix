{
  config,
  pkgs,
  lib,
  ...
}:
let
  scriptOpts = {
    options = {
      deps = lib.mkOption {
        type = with lib.types; listOf str;
        default = [ ];
        description = "List of dependencies. The script will run after these.";
      };
      text = lib.mkOption {
        type = lib.types.lines;
        description = "The content of the script.";
      };
    };
  };
  checkAssertWarn = lib.asserts.checkAssertWarn config.assertions config.warnings;
in
{
  options.system.topLevel = lib.mkOption {
    type = lib.types.path;
    description = "top-level system derivation";
    readOnly = true;
  };

  options.system.activation = {
    enable = lib.mkOption {
      type = lib.types.bool;
      default = true;
      description = ''
        Whether to enable system activation scripts.
      '';
    };

    scripts = lib.mkOption {
      type = with lib.types; attrsOf (coercedTo str lib.noDepEntry (submodule scriptOpts));
      default = { };

      example = lib.literalExpression ''
        { stdio.text =
          '''
            # Needed by some programs.
            ln -sfn /proc/self/fd /dev/fd
            ln -sfn /proc/self/fd/0 /dev/stdin
            ln -sfn /proc/self/fd/1 /dev/stdout
            ln -sfn /proc/self/fd/2 /dev/stderr
          ''';
        }
      '';

      description = ''
        A set of shell script fragments that are executed when a NixOS
        system configuration is activated.  Examples are updating
        /etc, creating accounts, and so on.  Since these are executed
        every time you boot the system or run
        {command}`nixos-rebuild`, it's important that they are
        idempotent and fast.
      '';
    };

    path = lib.mkOption {
      type = with lib.types; listOf package;
      default = [ ];
      description = ''
        Packages added to the `PATH` environment variable of activation scripts.
      '';
    };

    out = lib.mkOption {
      type = lib.types.path;
      description = "the actual script to run on activation....";
      readOnly = true;
    };
  };

  config = {
    system.activation.out =
      let
        set' = lib.mapAttrs (
          a: v:
          v
          // {
            text = ''
              #### Activation script snippet ${a}:
              _localstatus=0
              ${v.text}

              if (( _localstatus > 0 )); then
                printf "Activation script snippet '%s' failed (%s)\n" "${a}" "$_localstatus"
              fi
            '';
          }
        ) config.system.activation.scripts;
      in
      pkgs.writeScript "activate" ''
        #!${pkgs.runtimeShell}

        systemConfig='@systemConfig@'

        export PATH=/empty
        for i in ${toString config.system.activation.path}; do
            PATH=$PATH:$i/bin:$i/sbin
        done

        _status=0
        trap "_status=1 _localstatus=\$?" ERR

        # Ensure a consistent umask.
        umask 0022

        ${lib.textClosureMap lib.id set' (lib.attrNames set')}

        # Make this configuration the current configuration.
        # The readlink is there to ensure that when $systemConfig = /system
        # (which is a symlink to the store), /run/current-system is still
        # used as a garbage collection root.
        ln -sfn "$(readlink -f "$systemConfig")" /run/current-system

        exit $_status
      '';

    system.activation.scripts.specialfs = ''
      mkdir -p /run /tmp /var
      ln -sfn /run /var/run
    '';

    finit.tmpfiles.rules = lib.mkBefore [
      "d /etc"
      "d /run"
      "d /tmp 1777 root root"
      "d /var"
      "d /var/cache"
      "d /var/db"
      "d /var/empty"
      "d /var/lib"
      "d /var/log"
      "d /var/spool"
      "L+ /var/run - - - - /run"
    ];

    system.activation.path = map lib.getBin [
      config.programs.coreutils.package
      pkgs.gnugrep
      pkgs.findutils
      pkgs.getent
      pkgs.stdenv.cc.libc # nscd in update-users-groups.pl
      pkgs.shadow
      pkgs.nettools # needed for hostname
      pkgs.util-linux # needed for mount and mountpoint
    ];

    system.topLevel = checkAssertWarn (
      pkgs.stdenvNoCC.mkDerivation {
        name = "finix-system";
        preferLocalBuild = true;
        allowSubstitutes = false;
        buildCommand =
          let
            inherit (pkgs.buildPackages) coreutils;
          in
          ''
            mkdir -p $out $out/bin

            echo -n "finix" > $out/nixos-version

            cp ${config.system.activation.out} $out/activate

            substituteInPlace $out/activate --subst-var-by systemConfig $out

            ${coreutils}/bin/ln -sr ${config.boot.init} $out/init
            ${coreutils}/bin/ln -s ${config.environment.path} $out/sw
            ${coreutils}/bin/ln -s ${config.system.build.inhibitSwitch} $out/switch-inhibitors

            mkdir $out/specialisation

            ${lib.concatMapAttrsStringSep "\n" (
              k: v: "ln -s ${v.system.topLevel} $out/specialisation/${lib.escapeShellArg k}"
            ) config.specialisation}
          ''
          + lib.optionalString config.boot.kernel.enable ''
            ${coreutils}/bin/ln -s ${config.boot.kernelPackages.kernel}/${
              config.boot.kernelPackages.kernel.target or pkgs.stdenv.hostPlatform.linux-kernel.target
            } $out/kernel
            ${coreutils}/bin/ln -s ${config.system.modulesTree} $out/kernel-modules
            ${coreutils}/bin/ln -s ${config.hardware.firmware}/lib/firmware $out/firmware
          ''
          + lib.optionalString config.boot.initrd.enable ''
            ${coreutils}/bin/ln -s ${config.boot.initrd.package}/initrd $out/initrd
          ''
          + ''
            cp ${../../finit/switch-to-configuration.sh} $out/bin/switch-to-configuration
            substituteInPlace $out/bin/switch-to-configuration \
              --subst-var out \
              --subst-var-by shell ${config.environment.binsh} \
              --subst-var-by distroId finix \
              --subst-var-by finit ${config.finit.package} \
              --subst-var-by logger ${pkgs.util-linuxMinimal} \
              --subst-var-by coreutils ${config.programs.coreutils.package} \
              --subst-var-by installHook ${config.providers.bootloader.installHook} \
              --subst-var-by inhibitCheck ${config.system.build.checkSwitchInhibitors}
          ''
          + lib.optionalString config.boot.bootspec.enable ''
            ${config.boot.bootspec.writer}
          ''
          + lib.optionalString (config.boot.bootspec.enable && config.boot.bootspec.enableValidation) ''
            ${config.boot.bootspec.validator} "$out/${config.boot.bootspec.filename}"
          '';
      }
    );
  };
}
